//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthenaCN Script =====================================
//= 同个IP地址下, 最多在线玩家数的限制演示脚本
//===== By: ==================================================
//= Sola丶小克
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthenaCN 开发团队
//===== Description: =========================================
//= 演示如何通过 getsameipinfo 和 logout 指令来限制同IP下同时
//= 登录的玩家角色数量
//============================================================
//= 此脚本演示的指令为:
//= [脚本] getsameipinfo({<"想查找的IP地址">});
//= [脚本] logout <登出理由编号>{,"<角色名称>"|<账号编号>|<角色编号>};
//============================================================

-	script	RACN_IPLIMIT	-1,{
end;
OnPCLoginEvent:
	.@max_limit = 1;	// 每个IP最多登录的玩家个数
	
	// =========================================================
	// *getsameipinfo({<"想查找的IP地址">})
	// 这里“想查找的IP地址”是可选参数，若不填写则默认等于当前关联到脚本的玩家的IP地址
	// =========================================================
	
	getsameipinfo();
	
	// =========================================================
	// 调用 getsameipinfo(); 后, 系统会返回以下参数
	// @sameip_amount	-	IP地址相同的玩家数量(包括当前玩家在内)
	// @sameip_cid[]	-	IP地址相同的玩家的角色编号数组
	// @sameip_aid[]	-	IP地址相同的账号的账号编号数组
	// @sameip_name$[]	-	IP地址相同的角色名称数组(记住是字符串类型, 要有 $ 符号)
	// =========================================================
	if (@sameip_amount - 1 >= .@max_limit){
		mes "[多开限制检测]";
		mes "您触发了多开限制演示脚本,";
		mes "此演示NPC的脚本位于服务端的: ^ff0000npc\\racn\\racn_iplimit.txt^000000";
		mes "您可以在下面的配置文件中移除它: ^0000ffnpc\\scripts_racn.conf^000000";
		next;
		mes "[多开限制检测]";
		mes "您的IP地址为: ^ff0000" + getcharip() + "^000000.";
		mes "该IP已经有以下角色在线了:";
		
		for (.@i = 0; .@i < @sameip_amount; .@i++){
			if (@sameip_cid[.@i] != getcharid(0)){
				mes "- ^0055ff" + @sameip_name$[.@i] + "^000000";
			}
		}
		mes "本服每IP最多只有 ^ff0000" + .@max_limit + "^000000 个角色在线.";
		mes "伺服器拒绝您本次登录.";
		
		// 这里要等一会儿，避免用户还没loading完毕就被踢，体验好一些
		sleep2 2000;
		logout 4;	// 以 4 号理由将玩家踢下线, 可选的理由参数等请等待相关文档中说明
		close;
	}
end;
}