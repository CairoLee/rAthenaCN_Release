//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthenaCN Script =======================================
//= 快捷键设置演示脚本
//===== By: ==================================================
//= Sola丶小克
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthenaCN 开发团队
//===== Description: =========================================
//= 演示如何通过 gethotkey 和 sethotkey 指令来获取和设置快捷键
//============================================================
//= 此脚本演示的指令为:
//= [脚本] gethotkey <快捷键位置编号>{,<要获取的数据类型>};
//=        为了兼容 SeAr 脚本 get_hotkey 等效于 gethotkey
//= [脚本] sethotkey <快捷键位置编号>,<快捷键的类型>,<物品/技能的ID>,<技能等级>;
//=        为了兼容 SeAr 脚本 set_hotkey 等效于 sethotkey
//============================================================

prontera,130,125,3	script	快捷键设置	123,{
	mes "[rAthenaCN 演示]";
	mes "此演示NPC的脚本位于服务端的: ^ff0000npc\\racn\\racn_hotkey.txt^000000";
	mes "您可以在下面的配置文件中移除它: ^0000ffnpc\\scripts_racn.conf^000000";
	next;
	switch(select("列出全部快捷键详情:给两个独立快捷键设置上道具")){
		case 1:
			callfunc("F_HOTKEY_LIST_ALL_INFO");
		break;
		case 2:
			getitem 601,1;			// 获取一个苍蝇翅膀
			getitem 602,1;			// 获取一个蝴蝶翅膀
			sethotkey 36,0,601,0;	// 将苍蝇翅膀设置到36号快捷键位置上
			sethotkey 37,0,602,0;	// 将蝴蝶翅膀设置到37号快捷键位置上
		break;
	}
	end;
OnInit:
	waitingroom "快捷键设置演示",1;
	end;
}

function	script	F_HOTKEY_LIST_ALL_INFO	{
	for (.@i = 0; .@i <= 37; .@i++){
		.@ret = gethotkey(.@i);	// .@ret 中保存着调用函数之后的返回值
		
		// 如果返回值为 1 表示成功获取到了快捷键信息
		// 成功获取到信息后, 以下几个变量将保存被查询的快捷键信息
		// -------------------------------------------------------
		// @hotkey_type		快捷键的类型 - 0为物品, 1为技能
		// @hotkey_id		物品/技能的编号
		// @hotkey_lv		当快捷键类型为1时, 这里保存着快捷键中此技能的等级
		// -------------------------------------------------------
		if (.@ret == 1){
			.@mes$ = "[快捷位置编号: " + sprintf("%02d", .@i) + "] ";
			if (@hotkey_type == 0){
				.@mes$ = .@mes$ + "快捷键类型为 0 是一个物品 | ";
				.@mes$ = .@mes$ + "物品编号为 " + sprintf("%4d", @hotkey_id);
			}
			else {
				.@mes$ = .@mes$ + "快捷键类型为 1 是一个技能 | ";
				.@mes$ = .@mes$ + "技能编号为 " + sprintf("%4d", @hotkey_id) + " | ";
				.@mes$ = .@mes$ + "技能等级为 " + @hotkey_lv;
			}
			
			dispbottom .@mes$;
		}
		
		// 如果返回值为 -1 则表示获取失败
		if (.@ret == -1){
			dispbottom "[快捷位置编号: " + sprintf("%02d", .@i) + "] 获取快捷键信息失败.";
		}
	}
	return;
}