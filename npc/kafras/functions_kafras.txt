//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome){
		default:
		case 0:
			// Default message (obsolete)
			mes "[卡普拉 职员]";
			mes "欢迎光临!";
			mes "卡普拉公司永远为您提供最优质的服务!";
			mes "您需要什么服务呢?";
			break;
		case 1:
			// Niflheim specific message
			mes "[卡普拉 职员]";
			mes "^666666欢迎...来到...";
			mes "卡普拉公司...";
			mes "我们...到死...都会继续为您服务...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[卡普拉 职员]";
	 		mes "欢迎光临. ^ff0000" + GetGuildName(@GID) + "^000000 成员们.";
			mes "卡普拉公司永远在各位身边为您服务!";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[卡普拉 职员]";
			mes "您看起来好像来自一个遥远的地方, 是来研究我们的文化, 还是来观光呢?";
			mes "若两者皆是, 为何不稍稍停留片刻?";
			mes "在这野花香味扑鼻的国度, 相聚是缘.";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[卡普拉 职员]";
			mes "与许多的卡普拉服务点一样,";
			mes "我们会让你感觉从未离开家乡.";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	while (1) {
		next;
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (.@welcome == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"使用仓库","公会仓库","租用手推车","传送服务","结束对话";
		} else {
			switch(.@menu_num){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"储存服务","使用仓库","公会仓库","结束对话"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"使用仓库","公会仓库","结束对话"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"储存服务","使用仓库","公会仓库","租用手推车","查询其他信息","结束对话"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"使用仓库","公会仓库","租用手推车","查询其他信息","结束对话"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"使用仓库","公会仓库","查询其他信息","结束对话"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"储存服务","使用仓库","公会仓库","租用手推车","结束对话"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"储存服务","使用仓库","公会仓库","查询其他信息","结束对话"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"储存服务","租用手推车","传送服务","查询其他信息","结束对话"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"使用仓库","公会仓库","储存服务","租用手推车","结束对话"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"储存服务","使用仓库","传送服务","公会仓库","租用手推车","查询其他信息","结束对话"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "储存服务") {
			return;
		} else if (@K_Menu0$[.@j] == "使用仓库") {
			// Do not charge for Guild Storage
			if(.@welcome == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),.@welcome;
		} else if (@K_Menu0$[.@j] == "传送服务") {
			// Display Einbroch "No Teleport Service" notice.
			if(.@menu_num != 4) callfunc "F_KafTele",.@welcome;
			else {
				mes "[卡普拉 职员]";
				mes "由于卡普拉公司与秀发兹发德共和国";
				mes "签订了 ^FF0000《限制传送服务协议》^000000";
				mes "所以目前无法为您提供传送服务!";
				next;
				mes "[卡普拉 职员]";
				mes "如果您想移动到其他区域, 我们推荐您使用飞空艇服务!";
				mes "感谢您的理解与支持.";
			}
		} else if (@K_Menu0$[.@j] == "租用手推车") {
			callfunc "F_KafCart",.@welcome,getarg(4);
		} else if (@K_Menu0$[.@j] == "查询其他信息") {
			callfunc "F_KafInfo",getarg(2);
		} else if (@K_Menu0$[.@j] == "结束对话"){
			callfunc "F_KafEnd",.@welcome,0;
			end;
		} else if (@K_Menu0$[.@j] == "公会仓库") {
			callfunc "F_KafStor",1,0;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if(.@type == 1){
		switch(guildopenstorage()){
			case 0:
				mes "[卡普拉 职员]";
				mes "已经成功为您打开公会仓库!";
				break;
			case 1:
				mes "[卡普拉 职员]";
				mes "真的十分抱歉,";
				mes "目前有其他公会成员正在使用仓库!";
				mes "请过一段时间再尝试打开!";
				break;
			case 2:
				mes "[卡普拉 职员]";
				mes "很抱歉, 您至少需要加入一个公会,";
				mes "才可以使用公会仓库.";
				break;
		}
		cutin "", 255;
		close2;
		end;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(!callfunc("F_CanOpenStorage")){
		mes "[卡普拉 职员]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666对...不...起";
			mes "...你的...基本...技...能...等..级级..";
			mes "要...达到...6级...以上...才...可以...^000000";
			return;
		}
		mes "非常抱歉";
		mes "您的基本技能需要达到6级以上,";
		mes "才能使用仓库服务!";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(.@type != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<.@fee){
				mes "[卡普拉 职员]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666金金金币...";
					mes "需要...更..多的...金金...币...!";
					mes "至少...需要...150...金币...";
					mes "咕咚! 要不您先付一点血吧~!^000000";
					return;
				}
				// Standard Message
				mes "很抱歉, 您没有足够的金币,";
				mes "访问仓库至少需要 "+.@fee+" Zeny.";
				return;
			}
			set Zeny, Zeny-.@fee;
			set RESRVPTS, RESRVPTS + (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[卡普拉 职员]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666感谢..您的...使...用...^000000";
		}
		// Normal message
		else {
			mes "[卡普拉 职员]";
			mes "现在为您打开仓库!";
			mes "感谢您使用卡普拉服务.";
		}
	}
	callfunc("F_CheckKafCode", "[卡普拉 职员]");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[卡普拉 职员]";
	mes "请告诉我您的目的地";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "结束对话") 
	{
		mes "[卡普拉 职员]";
		mes "如果想用传送服务, 欢迎您随时来找我!";
		close2;
		cutin "", 255;
		end;
	}
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[卡普拉 职员]";
			mes "您没有足够的金币来使用传送服务,";
			mes "传送到 "+@wrpD$[.@j]+" 需要 "+@wrpP[.@j]+" Zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "艾尔帕兰") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "艾尔贝塔") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "克魔岛") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "发乐斯灯塔岛") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "吉芬") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "依斯鲁得岛") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "朱诺") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "妙勒尼废弃矿场") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "梦罗克") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "兽人村落") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "斐杨") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "普隆德拉") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "汶巴拉") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[卡普拉 职员]";
		mes "很抱歉, 手推车租用服务仅提供给";

		if (checkre(0))
			mes "商人系职业!";
		else {
			mes "商人、铁匠、炼金术师、创造者";
			mes "等商人系职业!";
		}
		close2;
		cutin "", 255;
		end;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[卡普拉 职员]";
		mes "你已经租用过手推车啦.";
		mes "我们不允许同时租用两辆手推车!";
		close2;
		cutin "", 255;
		end;
	}
	// Check if the player has the Pushcart skill
	else if(getskilllv("MC_PUSHCART") == 0) {
		mes "[卡普拉 职员]";
		mes "您必须先学习“手推车使用”技能,";
		mes "才可以租用手推车!";
		close2;
		cutin "", 255;
		end;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[卡普拉 职员]";
		mes "租用手推车需要 "+getarg(1)+" Zeny.";
		mes "您想继续租用手推车吗?";
		next;
		if (select("租用手推车:取消") == 2) 
		{
			mes "[卡普拉 职员]";
			mes "手推车可以让随身携带的物品数增加~";
			mes "也可以用来作为武器,";
			mes "如果想摆露天商店的话, 手推车也是必不可少的物品呢.";
			close2;
			cutin "", 255;
			end;
		}
		if(Zeny<getarg(1)){
			mes "[卡普拉 职员]";
			mes "很抱歉, 您并没有足够的金钱,";
			mes "租用手推车需要 "+getarg(1)+" Zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny,Zeny-getarg(1);
		set RESRVPTS, RESRVPTS + 48;
	}
	mes "[卡普拉 职员]";
	mes "手推车租用手续已经登记完毕~";
	mes "要好好爱惜它哟.";
	setcart;
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "查看储蓄点数","仓库密码服务","职员位置分布","结束对话";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "查看储蓄点数") {
			mes "[卡普拉 职员]";
			mes "让我看一下...";
			mes "您的名字是..." + strcharinfo(0) + "...";
			mes "储蓄点数一共是 ^FF0000" + RESRVPTS + " ^000000点!";
			next;
			mes "[卡普拉 职员]";
			mes "在艾尔帕兰的卡普拉公司总部,";
			mes "您可以把储蓄点数换成赠品,";
			mes "请多使用无比方便的卡普拉服务, 换取更多的储蓄点数哟!";
			close2;
			cutin "", 255;
			end;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "职员位置分布") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			mes "[卡普拉 职员]";
			mes "已经在您的小地图上标出了本城镇";
			mes "所有卡普拉职员的位置.";
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			mes "[卡普拉 职员]";
			mes "看完啦, 记住了吗?";
			mes "那我就将小地图上的标记擦掉啦,";
			mes "如果记不住的话, 可以再来找我哟!";
			close2;
			cutin "", 255;
			end;
		} else if (.@m$[.@j] == "仓库密码服务") {
			callfunc("F_SetKafCode","[卡普拉 职员]","我们卡普拉公司");
		} else if (.@m$[.@j] == "结束对话") {
			mes "[卡普拉 职员]";
			mes "如果您还有不清楚的地方,";
			mes "欢迎询问离你最近的卡普拉职员.";
			close2;
			cutin "", 255;
			end;
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[卡普拉 职员]";
	if(getarg(1)==1) {	
		// Save
		mes "您的复活点已经存储到了:";
		mes  "^0000FF"+getarg(2)+"^000000";
		mes "谢谢您使用我们卡普拉公司的服务!";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "为了让您舒心的使用卡普拉公司的服务, 我们会尽最大努力提升我们的服务质量.";
		mes "感谢您对我们一如既往的支持.";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666我们...卡普拉...公司...";
		mes "会一直...陪伴在...您的...身边...";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "请输入您的仓库密码:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		next;
		mes getarg(0);
		mes "很抱歉, 仓库密码输入错误!";
		mes "无法为您打开仓库.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if(#kafra_code) {
		mes "您曾经申请过使用密码保护仓库的服务.";
		mes "请问现在想要做什么事情呢?";
		next;
		switch (select("修改仓库密码 -> 5000z:取消仓库密码 -> 1000z:结束对话")) {
			case 1:
				mes .@npc_name$;
				mes "首先, 请输入您的 ^0000FF旧密码(现有密码)^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "呃..密码错误. 您不能修改仓库密码!";
					emotion ET_SCRATCH;
					break;
				}
				next;
				callsub S_SET,.@npc_name$,.@comp_name$;
				break;
			case 2:
				mes .@npc_name$;
				mes "在取消仓库密码之前,";
				mes "请输入之前设定的密码来确认您的身份.";
				mes "==============================";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "您的仓库密码尚未被移除!";
					emotion ET_SCRATCH;
					break;
				}
				next;
				mes .@npc_name$;
				if(Zeny < 1000) {
					mes "您没有足够的钱用来取消仓库密码!";
					emotion ET_MONEY;
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "您的仓库密码已经移除";
					mes "感谢您使用"+.@comp_name$+"的服务.";
					emotion ET_THANKS;
				} else {
					mes "密码错误!";
					mes "您上交的 1000z 我们将不予退回!";
					mes "请想好正确的密码后, 再来找我吧.";
					emotion ET_SORRY;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes .@comp_name$+"现已经推出全新服务:";
		mes "^0000FF使用密码来加强仓库的安全性^000000";
		next;
		switch (select("设置一个新密码 -> 5000z:结束对话")) {
			case 1: callsub S_SET,.@npc_name$,.@comp_name$; break;
			case 2: 
				mes .@npc_name$;
				mes "您真的不想用吗?";
				mes "设定好仓库密码后就算游戏账号被盗,";
				mes "坏蛋也拿不走仓库的东西哟!";
				close2;
				cutin "", 255;
				end;
			break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes .@npc_name$;
	mes "现在, 我们来设定^FF0000新密码^000000以便保护仓库!";
	set @code,callfunc("F_EntKafNewCode");
	if(!@code) {
		mes "密码已经设定完毕!";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if(Zeny < 5000) {
		mes "您没有足够的金钱!";
		emotion ET_MONEY;
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "您的仓库密码已启用!";
	mes "感谢您使用"+.@comp_name$+"的服务.";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "请输入仓库密码:";
	set @code_,0;
	input @code_;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Tried to fit storage password.";
	}
	return @code_;
}

function	script	F_EntKafNewCode	{
	mes "请输入一个4-8位数字之间的数字";
	mes "来作为您的仓库密码:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>3) {
		set @kafcode_try,0;
		logmes "Hack: Tried to fit storage password.";
	}
	if(input(@code_) == 1) {
		next;
		mes "[卡普拉 职员]";
		mes "汗, 您的密码好短啊, 不安全呀!";
		mes "请至少输入一个4位数的密码.";
		close2;
		cutin "", 255;
		end;
	}
	if(@code_<1000) {
		next;
		mes "[卡普拉 职员]";
		mes "您输入的密码太短了, 不安全呀!";
		mes "请至少输入一个4位数的密码.";
		close2;
		cutin "", 255;
		end;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "依斯鲁得岛", "吉芬", "斐杨", "梦罗克", "兽人村落", "艾尔贝塔";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "斐杨", "梦罗克", "普隆德拉";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "吉芬", "朱诺" , "依斯鲁得岛", "妙勒尼废弃矿场";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "梦罗克", "发乐斯灯塔岛", "汶巴拉";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "克魔岛", "梦罗克";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "普隆德拉", "艾尔帕兰", "兽人村落", "妙勒尼废弃矿场";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "吉芬", "斐杨", "梦罗克", "艾尔帕兰";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "普隆德拉", "斐杨", "艾尔贝塔", "克魔岛", "发乐斯灯塔岛";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "克魔岛";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "普隆德拉", "艾尔贝塔", "梦罗克";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "艾尔帕兰";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "依斯鲁得岛", "吉芬", "斐杨", "梦罗克", "艾尔帕兰";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
			setd "@wrpP["+.@i+"]",getd("@wrpP["+.@i+"]") * 2;
	}

	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]")+" z";
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","结束对话";
	return;
}
