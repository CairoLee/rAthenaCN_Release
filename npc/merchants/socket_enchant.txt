//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- 等一下!! -";
		mes "- 目前您携带的物品已经太多了 -";
		mes "- 请减少一下负重之后 -";
		mes "- 再来找我吧 -";
		close;
	}
	mes "[世雅布莲]";
	mes "我是一位专业打孔工程师.";
	mes "打孔能为普通武器和防具增加拓展卡槽.";
	mes "我的收费以及打孔所需的材料和成功率, 根据装备的不同也会有所不同.";
	next;
	mes "[世雅布莲]";
	mes "打孔看起来好像很简单, 但实际操作却比看起来复杂很多.";
	mes "如果您对打孔感兴趣, 就来找我吧.";
	next;
	switch(select("为武器打孔:为防具打孔:再听听打孔说明:取消"))
	{
		case 1:
			mes "[世雅布莲]";
			mes "您想为武器打孔吗?";
			mes "我按照武器的打孔难易度进行了归类, C级是最容易的, S级是最困难的.";
			mes "您想尝试哪个分类?";
			next;
			switch(select("C级:B级:A级:S级"))
			{
				case 1:
					mes "[世雅布莲]";
					mes "嗯? C级?";
					mes "您希望我为哪一种C级武器打孔呢?";
					next;
					switch(select("德瑞尹长戟:鞭子:小提琴"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[世雅布莲]";
					mes "B级难度?";
					mes "好的, 这是难易度适中的装备类型.";
					mes "您希望我为哪一种B级武器打孔呢?";
					next;
					switch(select("链锤:长柄短剑:角弓:步兵长矛:海东剑:鲁特琴:钢铁鞭子:虎爪拳套:埃布尔雷弓"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[世雅布莲]";
					mes "哇, A级难度.";
					mes "这个类别的风险可是会有点高哟!";
					mes "您希望我为哪一种A级武器打孔呢?";
					next;
					switch(select("猎人之弓:高等生存的魔杖(INT):巨大双手剑:水纹之剑:刺杀拳刃:贝利斯塔强弩:昏迷之锤:秘拳套 闪光:十字巨剑"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[世雅布莲]";
					mes "哇, 哇, 哇...";
					mes "S级难度? 您确定?...";
					mes "如果我真的能做成这件事, 这将是一个奇迹.";
					mes "您希望我为哪一种S级武器打孔呢?";
					next;
					switch(select("刚尼尔长矛:巨毒短剑:破甲短锥:砂尘短剑:卡卡普剑:狂击之剑:死神之镰:生存的魔杖(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[世雅布莲]";
			mes "防具嘛?";
			mes "可以, 当然没问题.";
			mes "防具根据打孔难度的不同, 我将防具被分为四个不同等级.";
			mes "C级是最简单的, S级是最困难的.";
			mes "您想尝试哪个分类?";
			next;
			switch(select("C级:B级:A级:S级"))
			{
				case 1:
					mes "[世雅布莲]";
					mes "C级防具, 对么?";
					mes "听起来很合理的嘛.";
					mes "您想尝试给什么防具打孔呢?";
					next;
					switch(select("大衣:长大衣:头箍:圣职之帽"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[世雅布莲]";
					mes "您选择了难易度适中的B级防具.";
					mes "那么, 想尝试给什么防具打孔呢?";
					next;
					switch(select("镜盾:钢铁锁子甲:圣职之衣:丝质外袍:战士长靴:长靴:披肩:铁盾:圆盾:钢盾:妖道帽"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[世雅布莲]";
					mes "哇, A级防具.";
					mes "这个类别的风险可是会有点高哟!";
					mes "好吧, 您想尝试给什么防具打孔呢?";
					next;
					switch(select("宝石头盔:软呢帽:谎言纪录本:紧身便衣:秘银金属铠甲:钢铁铠甲:盗贼之衣:重靴:修女的帽子:斗篷:金属头盔:忍服 血樱:兽人头盔:伯爵斗篷:神圣之帽:黄金帽:敏捷别针:僵尸帽"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[世雅布莲]";
					mes "呃... S级防具? 您确定?";
					mes "好吧... 您想尝试给什么防具打孔呢? 我会告诉您需要什么材料.";
					next;
					switch(select("山羊头盔:尖角发圈:骨制头盔:海盗船长帽:王冠:后冠:人面狮身帽:创作者外袍:智力耳环:力量戒指:神射手套"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[世雅布莲]";
			mes "我的打孔技术还达不到艺术家级别.";
			mes "它非常复杂, 如果我想说谎的话, 那么我会说“我知道打孔过程中每一个因素带来的后果”, 但我是个诚实的人.";
			mes "不过, 我倒是留意到几个趋势...";
			next;
			mes "[世雅布莲]";
			mes "在某些城镇中, 装备精炼值越低则打孔成功率越高.";
			mes "而在另外一些城镇, 则恰恰相反.";
			mes "你不觉得这件事很奇怪么?";
			next;
			mes "[世雅布莲]";
			mes "噢, 还真有些重要的事情得让您知道.";
			mes "如果您请求我给某种装备打孔的话, 请确保您的背包中没有与它同名的装备.";
			next;
			mes "[世雅布莲]";
			mes "例如: 您背包中携带着一件 +7 的斗篷和一件没精炼的斗篷. 那么我将随机选取一个斗篷来打孔.";
			mes "请记住一定要非常小心.";
			next;
			mes "[世雅布莲]";
			mes "记住, ^FF0000只带一个要打孔的装备在身上^000000.";
			mes "其他同名的装备都请先放在卡普拉仓库, 明白了吗?";
			close;
		case 4:
			mes "[世雅布莲]";
			mes "放轻松, 冒险家. ";
			mes "如果您想要尝试给装备打孔, 那么请将您的装备穿回来给我看看.";
			mes "再见~";
			close;
	}
}

function	script	Func_Socket	{

	disable_items;
	mes "[世雅布莲]";
	mes "您想要尝试为 "+ getitemname(getarg(0))+ " 打孔对么?";
	mes "好吧, 那么请带给我";
	mes "^FF0000" + getarg(6) + " 个 " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "":"") + "^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getarg(8,0) + " " + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "":"") + "^000000 ") : "") + "和 ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 手续费^000000. ";
	mes "对了, 别忘记带上要打孔的 " + getitemname(getarg(0)) + "!";
	next;
	mes "[世雅布莲]";
	if (getiteminfo(getarg(0),5)&2) // EQP_HAND_R = 2, it's a weapon
	{
		mes "如果您准备了我需要的物品和费用, 我会尝试在您的武器上打孔.";
		mes "在那之前, 我必须告诉您相关的风险.";
		next;
		mes "[世雅布莲]";
		mes "如果尝试打孔到您的武器时失败了, 那么^FF0000武器^000000及^FF0000上面的卡片^000000, 都将被^FF0000摧毁^000000.";
	}
	else // armor
	{
		mes "如果您准备了我需要的物品和费用, 我会尝试在您的防具上打孔. ";
		mes "在那之前, 我必须告诉您相关的风险.";
		next;
		mes "[世雅布莲]";
		mes "如果尝试打孔到您的防具时失败了, 那么^FF0000防具^000000及^FF0000上面的卡片^000000, 都将被^FF0000摧毁^000000.";
	}
	mes "您还想继续尝试打孔吗?";
	next;
	switch(select("继续尝试打孔:取消"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[世雅布莲]";
				mes "好吧, 我开始工作了!";
				mes "请您祈祷我的工作一帆风顺吧.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[世雅布莲]";
					mes "太好了, 成功了.";
					mes "它看起来非常完美. 恭喜您!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[世雅布莲]";
					mes "希望下次还能再见到您!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[世雅布莲]";
					mes "哇! ...很抱歉, 打孔失败了.";
					mes "不过, 我是完全无辜的.";
					mes "这是您的运气, 以及上帝的安排, 明白吗?";
					mes "别那么失望, 下次再来试试吧.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[世雅布莲]";
					mes "我祝您下次能有好运气!";
					close;
				}
			}
			else
			{
				mes "[世雅布莲]";
				mes "你好像缺少一些东西吧.";
				mes "你确信你有需要打孔的装备, 以及所需的材料和足够的费用吗?";
				close;
			}
		case 2:
			mes "[世雅布莲]";
			mes "需要一些时间来想想嘛, 嗯?";
			mes "好吧, 我可以完全明白.";
			mes "记住, 如果人生在世不冒险的话, 生活是不会有乐趣的~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	世雅布莲#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	世雅布莲#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	世雅布莲#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	世雅布莲#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[年轻人]";
	mes "我在考虑成为一个打孔工程师.";
	mes "它可以帮您的装备添加新的拓展卡槽, 如果我能掌握这种有利可图的能力, 那真是帅到家了.";
	next;
	mes "[年轻人]";
	mes "根据我的理解, 想要成功给装备打孔, 您的个人运气是非常重要的因素.";
	mes "有时虽然会失败, 但是幸运的话, 有时一次性能给装备打上两个拓展卡槽.";
	mes "很疯狂. 不是么?";
	next;
	mes "[年轻人]";
	mes "不过不要太激动.";
	mes "只有部分特定类型的装备, 才有机会同时打上两个拓展卡槽.";
	mes "在我成为学徒之前, 我不得不学习这么多关于打孔的知识...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	年轻人#prt	97
payon,143,143,7	duplicate(SocketMan)	年轻人#pay	97
morocc,60,42,3	duplicate(SocketMan)	年轻人#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	年轻人#lhz	97
