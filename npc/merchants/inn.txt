//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script =======================================
//= Inn Npcs
//===== By: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: =====================================
//= 2.9a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//============================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	旅馆 服务生#Sammy	53,{

	callfunc "F_InnMaid","[服务生 善美]","内卡拉司旅馆","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	旅馆 服务生#Ahlma	53,{

	callfunc "F_InnMaid","[服务生 阿玛]","内卡拉司旅馆","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	服务员 珍妮#Jennie	53,{

	callfunc "F_InnMaid","[服务员 珍妮]","琵秀旅馆","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	旅馆 服务生#Cena	53,{

	callfunc "F_InnMaid","[服务生 赛娜]","伊芙丽特旅馆","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	旅馆 服务生#Ahee	53,{

	callfunc "F_InnMaid","[服务生 阿喜]","斐扬旅馆","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	旅馆 服务生#Hasna	53,{

	callfunc "F_InnMaid","[服务生 汉斯娜]","梦罗克旅馆","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	旅馆 服务生#Manar	53,{

	callfunc "F_InnMaid","[服务生 马纳尔]","梦罗克旅馆","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	旅馆 服务生#Rilim	53,{

	callfunc "F_InnMaid","[服务生 立琳]","艾尔帕兰旅馆","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	饭店 职员#Annie	931,{

	callfunc "F_InnMaid","[饭店 职员]","拉赫饭店","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	饭店 职员#01	86,{
	mes "[饭店 职员]";
	mes "欢迎光临，我们是业界最高级的饭店。";
	mes "叫做“皇龙”饭店。";
	mes "我们拥有宽敞的空间，";
	mes "提供最舒适的休息环境，";
	mes "让顾客宾至如归，保证满意！";
	next;
	switch(select("储存服务:休息 - 5,000 金币:结束对话")) {
	case 1:
		savepoint "lhz_in02",209,275;
		mes "[饭店 职员]";
		mes "您的复活点已经存储在皇龙饭店啦。";
		mes "十分感谢您对我们的信任。";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[饭店 职员]";
			mes "很抱歉，服务费需要5,000金币。";
			mes "请确认你有足够的金币后再来找我，好吗？";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[饭店 职员]";
		mes "对您致以最诚挚的谢意~";
		mes "希望您能享受“皇龙”给您带来的休息时光。";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[饭店 职员]";
		mes "感谢您的光临，希望你能过得愉快。";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	旅馆 员工#Receptionist	53,{

	callfunc "F_InnMaid","[接待员]","毁葛旅馆","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	旅馆 老板#Receptionist	709,{
	mes "[旅馆 老板]";
	mes "欢迎光临自由自在的菲音斯旅馆~";
	next;
	switch(select("储存服务:休息 -> 5000 金币:结束对话")) {
	case 1:
		mes "[旅馆 老板]";
		mes "您的复活点已经存储在菲音斯。";
		mes "尽情享受这个城镇带来的风光吧~";
		savepoint "ve_in",157,209;
		close;
	case 2:
		mes "[旅馆 老板]";
		if(Zeny < 5000){
			mes "很抱歉，服务费需要5,000金币。";
			mes "请确认你有足够的金币后再来找我，好吗？";
			close;
		}
		mes "放满脚步，多在这个城镇停留一下~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[旅馆 老板]";
		mes "欢迎下次光临~";
		close;
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	mes getarg(0);
	mes "欢迎光临" + getarg(1) + "！";
	mes "请问有什么需要我帮忙的嘛？";
	next;
	switch(select("储存服务:休息 -> 5000 金币:结束对话")) {
	case 1:
		mes getarg(0);
		mes "您的复活点已经存储完毕。";
		mes "欢迎您下次光临。";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		mes getarg(0);
		if(Zeny < 5000){
			mes "很抱歉，服务费需要5,000金币。";
			mes "请确认你有足够的金币后再来找我，好吗？";
			close;
		}
		mes "谢谢惠顾。";
		mes "希望您能好好休息~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
