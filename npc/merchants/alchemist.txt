//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//============================================================ 

alde_alche,24,188,3	script	公会销售员	740,{
	if (checkweight(1201,1) == 0) {
		mes "- 请等等! -";
		mes "- 你身上携带了太多物品了 -";
		mes "- 先把东西多余的东西 -";
		mes "- 寄放在卡普拉仓库之后 -";
		mes "- 再回来吧! -";
		close;
	}
	mes "[葛伯 纱普]";
	mes "欢迎来到炼金术师组织，";
	mes "需要什么服务吗?";
	next;
	switch (select("购买制药物品:购买制作说明书:取消")) {
	case 1:
		mes "[葛伯 纱普]";
		mes "请问需要什么物品";
		next;
		if (select("药钵 - 8 Zeny:停止交易") == 1) {
			mes "[葛伯 纱普]";
			mes "请问需要几个?";
			mes "如想要停止交易请写 '0' ";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[葛伯 纱普]";
					mes "交易已经取消，下次再说吧";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[葛伯 纱普]";
					mes "单次购买数量必须是1至2000个之间的数";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[葛伯 纱普]";
				mes "金额不足噢";
				mes "请确认一下所携带的金额";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[葛伯 纱普]";
				mes "看样子都不能带走啊，";
				mes "请确认一下所携带的物品量和负重";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[葛伯 纱普]";
			mes "感谢你的购买，";
			mes "愿你做出实用的药";
			close;
		}
		mes "[葛伯 纱普]";
		mes "好吧~";
		mes "如果你需要制药相关的道具，可以随时找我~";
		close;
	case 2:
		mes "[葛伯 纱普]";
		mes "请问需要哪些物品?";
		mes "一般的制作说明书是100,000金币，";
		mes "比较特殊的几种制作说明书除外";
		next;
		switch(select("一般药水制作说明书:酒精制作说明书:火烟瓶制作说明书:盐酸制作说明书:植物瓶制作说明书:刺锤瓶制作说明书:护贝药制作说明书:纤细药水制作说明书:取消交易")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[葛伯 纱普]";
			mes "好吧~";
			mes "如果你需要制药相关说明书的话，可以随时找我~";
			close;
		}
	case 3:
		mes "[葛伯 纱普]";
		mes "好滴，祝你有个美好的一天~";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[葛伯 纱普]";
		mes "金额不足噢";
		mes "请确认一下所携带的金额";
		close;
	}
	mes "[葛伯 纱普]";
	mes ""+getitemname(getarg(0))+"?";
	mes "购买这本说明书需要 "+getarg(1)+" zeny.";
	next;
	if (select("确定购买:不买了") == 1) {
		set Zeny, Zeny-getarg(1);
		getitem getarg(0),1;
		mes "[葛伯 纱普]";
		mes "谢谢惠顾~";
		mes "愿你做出实用的药";
		close;
	}
	mes "[葛伯 纱普]";
	mes "好的，那么请下次再来吧！";
	close;
}
