//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================ 

// PecoPeco Breeder (for Knights) :: breeder
//============================================================
prontera,55,350,5	script	大嘴鸟饲养员(骑士)#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[大嘴鸟饲养员]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "你好, 荣耀的骑士呀, 欢迎光临.";
		mes "你想要租用一只可爱的大嘴鸟吗?";
		mes "租赁费用是 ^FF0000"+.@price+"^000000 金币.";
		next;
		switch(select("是的, 我想租一只大嘴鸟:取消")) {
		case 1:
			if (Zeny < .@price) {
				mes "[大嘴鸟饲养员]";
				mes "可是你没有足够的金钱哟.";
				mes "难道...你破产了?";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鸟饲养员]";
				mes "很抱歉, 你无法使用本服务, 因为你根本控制不了大嘴鸟.";
				mes "请你先学习技能列表中的^0000FF骑乘术^000000, 然后再来找我吧.";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鸟饲养员]";
				mes "你已经租用了一只可爱的大嘴鸟了哟.";
				mes "暂时不能租第二只啦 ^-^";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鸟饲养员]";
				mes "哟, 你还乘坐着商城坐骑呢! 请先从商城坐骑身上下来吧, 不然你租了大嘴鸟也没用的呀.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			mes "[大嘴鸟饲养员]";
			mes "好滴, 这是你的大嘴鸟, 请善待它哟.";
			close;
		case 2:
			mes "[大嘴鸟饲养员]";
			mes "噢..我明白了.";
			mes "祝你有个好心情, 再见啦.";
			close;
		}
	} else {
		mes "你好, 噢...? 很抱歉哈.";
		mes "大嘴鸟只能租给^0000FF骑士^000000或^0000FF骑士领主^000000.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)
//============================================================
prontera,232,318,3	script	大嘴鸟饲养员(十字军)#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[大嘴鸟饲养员]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "欢迎光临, 十字军.";
		else mes "欢迎光临, 圣殿十字军.";
		mes "我们为你准备了特殊的大嘴鸟.";
		mes "租用一只需要 ^FF0000"+.@price+"^000000 金币.";
		next;
		switch(select("太好了, 租给我一只大嘴鸟吧:取消")) {
		case 1:
			if (Zeny < .@price) {
				mes "[大嘴鸟饲养员]";
				mes "你没有足够的金钱来支付租金呢.";
				mes "如果你想要租一只可爱的大嘴鸟的话, 请带来 ^FF0000"+.@price+"^000000 金币...";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鸟饲养员]";
				mes "你必须先学会^0000FF骑乘术^000000, 然后我才能租给你大嘴鸟.";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鸟饲养员]";
				mes "你已经租用了一只可爱的大嘴鸟了哟.";
				mes "暂时不能租第二只啦 ^-^";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鸟饲养员]";
				mes "哟, 你还乘坐着商城坐骑呢! 请先从商城坐骑身上下来吧, 不然你租了大嘴鸟也没用的呀.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			mes "[大嘴鸟饲养员]";
			mes "好滴, 这是你的大嘴鸟, 请善待它哟.";
			close;
		case 2:
			mes "[大嘴鸟饲养员]";
			mes "那么...回见 ^-^";
			close;
		}
	} else {
		mes "我能为您做些什么呢?";
		mes "大嘴鸟的租赁要求是十分严格的, 必须是^0000FF十字军^000000或者^0000FF圣殿十字军^000000才能租赁.";
		close;
	}
}

// Falcon Master :: breeder
//============================================================
hu_in01,381,304,5	script	猎鹰饲养员#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[猎鹰饲养员]";
	if (BaseJob == Job_Hunter) {
		if (checkwug()) {
				mes "呃.. (⊙o⊙)...";
				mes "在解散你召唤的野狼之前,";
				mes "你不能租赁猎鹰!";
				mes "因为她们说不定会打起来.";
			close;
		}
		mes "你好, 冒险家, 想租赁猎鹰吗?";
		mes "只需 "+.@price+" 金币,";
		mes "就可以租到值得信赖的猛禽哟~";
		next;
		switch(select("太棒了, 我刚好想租一只:取消")) {
		case 1:
			if (checkwug()) {
				mes "[猎鹰饲养员]";
				mes "呃.. (⊙o⊙)...";
				mes "在解散你召唤的野狼之前,";
				mes "你不能租赁猎鹰!";
				mes "因为她们说不定会打起来.";
				close;
			}
			if (Zeny < .@price) {
				mes "[猎鹰饲养员]";
				mes "这是什么情况?";
				mes "你没有足够的钱哇?!";
				mes "赶快开始狩猎魔物赚钱吧~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[猎鹰饲养员]";
				mes "我的天哪~";
				mes "你必须先学会^0000ff驯鹰术^000000才行!";
				mes "如果你控制不了猎鹰,";
				mes "我是不放心把她们租给你的,";
				mes "明白我的意思了吧?";
				close;
			}
			else if(checkfalcon()) {
				mes "[猎鹰饲养员]";
				mes "呃.. (⊙o⊙)...";
				mes "你已经有一只猎鹰了.";
				mes "往你的头上看,";
				mes "她在你的头上盘旋飞翔哟...";
				close;
			}
			set Zeny, Zeny-.@price;
			setfalcon;
			mes "[猎鹰饲养员]";
			mes "好的, 这是你的猎鹰, 请善待它哟.";
			close;
		case 2:
			mes "[猎鹰饲养员]";
			mes "等、等一下, 你想去哪?";
			mes "我用人格担保, 这些都是一流的猎鹰!";
			mes "来吧, 哟~";
			close;
		}
	} else {
		mes "年轻的傻瓜!";
		mes "猎鹰只有猎人、游侠系列职业能使用,";
		mes "明白了吗? ...嘿嘿, 很羡慕吧?";
		close;
	}
}
