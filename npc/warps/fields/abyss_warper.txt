//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script =======================================
//= Abyss Cave Warper
//===== By: ==================================================
//= erKURITA
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Warper to Abyss Cave. and warper out
//===== Additional Comments: =================================
//= 1.0 Added by Nexon [Nexon]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Fixed tab - missing pillar's appeared [Lupus]
//= 1.3 Updated Abyss Lake entrance based on official [SinSloth]
//= 1.3a Removed .GATs [Lupus]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//===== rAthenaCN Dev: =======================================
//= 2016.02.13 英文直译对话部分至中文 [杨梅]
//============================================================

hu_fild05,168,304,0	script	柱子#abyss1	111,{
	mes "^3355FF- 柱子的表面可以看到 -";
	mes "- 三个类似龙身部分的凹槽 -";
	mes "- 好像是龙的牙、鳞片与尾巴的部分 -";
	mes "- 每个凹槽似乎都需要合适的道具 -^000000";
	if((countitem(1035)) && (countitem(1036)) && (countitem(1037)))
	{
		next;
		switch( select( "将龙身上的部件放进凹槽","将龙身上的部件直接吃掉" ) )
		{
			case 1:
				mes "^3355FF- 将龙牙的部分放了进去 -";
				mes "- 从柱子听到一阵隆隆声 -^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FF- 将龙鳞片的部分放进了另一个凹槽 -";
				mes "- 柱子摇晃了一下 -^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FF- 你小心的将龙尾部分也放了进去 -";
				mes "- 从柱子的表面发出了一阵光芒 -^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FF- 你的脚开始陷进了地面 -";
				mes "- 并猛烈的晃动 -^000000";
				specialeffect EF_BOWLINGBASH;
				donpcevent "AbyssWarp::OnWarp";
				specialeffect2 EF_PORTAL;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				close2;
				warp "hu_fild05",184,204;
				end;

			case 2:
				mes "^3355FF- 你小心地将龙牙、龙鳞片 -";
				mes "- 以及龙尾巴的部分放进口中 -";
				mes "- 并开始咀嚼 -^000000";
				mes "^FF0000- 什么都没有发生 -^000000";
				mes "^3355FF- 只是尝到了腐烂的味道 -^000000";
				next;
				emotion e_rice,1;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				percentheal -10,0;
				close;
		}
	}
	close;
}

hu_fild05,171,211,0	script	柱子#abyss2	111,1,1,{

	mes "^3355FF- 这根柱子和湖边看到过的柱子类似 -";
	mes "- 凹槽的地方发出蓝色的光芒 -^000000";
	next;
	switch( select( "触摸发光的地方","从发光的凹槽中取出物品" ) )
	{
		case 1:
			mes "^3355FF*哐当哐当*^000000";
			next;
			mes "^3355FF- 凹槽中的光芒分散的同时 -";
			mes "- 柱子也开始不断的颤动 -";
			mes "- 你听到了从柱子内部传来的轰鸣声 -^000000";
			specialeffect EF_WINDHIT;
			next;
			mes "*嘎吱嘎吱!*";
			mes "*哐!*";
			next;
			mes "^3355FF- 凹槽发出的光芒变得非常强烈 -";
			mes "- 脚下的地面开始慢慢的沉了下去 -^000000";
			next;
			specialeffect EF_BOWLINGBASH;
			specialeffect2 EF_PORTAL;
			close2;
			warp "hu_fild05",169,305;
			end;

		case 2:
			mes "^3355FF- *噗!* -";
			mes "- 取出来的物品都碎掉了 -^000000";
			specialeffect EF_WINDHIT;
			next;
			mes "^3355FF*哐哐哐!*^000000";
			next;
			mes "^3355FF- 凹槽中的光芒开始扭曲 -";
			mes "- 柱子也开始不断的颤动 -";
			mes "- 震动的地面让柱子难以站立 -";
			mes "- 在一阵扭曲之后 你被传送了出去 -^000000";
			next;
			specialeffect EF_BOWLINGBASH;
			specialeffect2 EF_PORTAL;
			close2;
			warp "hu_fild05",157,284;
			end;
	}
}

hu_fild05,196,210,1	script	AbyssWarp	45,2,2,{

OnInit:
	disablenpc "AbyssWarp";
	end;

OnTouch_:
	warp "abyss_01",260,268;
	end;

OnWarp:
	initnpctimer;
	enablenpc "AbyssWarp";
	end;

OnTimer30000:
	stopnpctimer;
	disablenpc "AbyssWarp";
	end;
}
