//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script =======================================
//= Prontera Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.6a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Prontera
//===== Additional Comments: =================================
//= 1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.6a Fixed some errors with the loop. [Samuray22]
//	-Thanks to $ephiroth.
//= 1.6b Fixed a Problem with Unnecesary RGB Values. [Samuray22]
//	-Thanks to Brainstorm.
//============================================================

prontera,154,187,4	script	导游#prt::PrtGuide	105,{
	cutin "prt_soldier",2;
	mes "[普隆德拉导游]";
	mes "欢迎来到普隆德拉,";
	mes "卢恩米德加兹王国的美丽首都.";
	mes "当您有问题或想寻找某些城市设施时,";
	mes "欢迎随时来问我.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("城市导览:删除小地图的标记:注意事项:结束对话")) {
		case 1:
			mes "[普隆德拉导游]";
			mes "请在菜单中选择您想去的位置.";
			if (.@compass_check == 0) {
				mes "是否需要我在小地图中,";
				mes "把您想去的地方做上标记呢?";
				next;
				if (select("需要:不需要") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("剑士公会:^0000FF教堂^000000:普隆德拉骑士团:武器店:道具店:旅馆:贸易站:酒馆:图书馆:职业介绍所:普隆德拉城堡:城市会所:取消")) {
				case 1:
					mes "[普隆德拉导游]";
					mes "剑士公会主要负责剑士职业的转职,";
					mes "现在已搬到了依斯鲁德岛.";
					mes "这个设施现在已经是一座空建筑了.";
					if (.@compass_check)
						viewpoint 1,237,41,4,0x00FF00;
					break;
				case 2:
					mes "[普隆德拉导游]";
					mes "普隆德拉教堂主要负责服事职业的转职,";
					mes "位于普隆德拉东北方向的角落.";
					if (.@compass_check)
						viewpoint 1,236,316,5,0xFF0000;
					break;
				case 3:
					mes "[普隆德拉导游]";
					mes "骑士团负责我们首都的安全,";
					mes "位于普隆德拉西北方向的角落.";
					if (.@compass_check)
						viewpoint 1,46,345,6,0x00FF00;
					break;
				case 4:
					mes "[普隆德拉导游]";
					mes "武器店位于中央喷泉区的东北方向.";
					if (.@compass_check)
						viewpoint 1,175,220,7,0xFF00FF;
					break;
				case 5:
					mes "[普隆德拉导游]";
					mes "道具店位于中央喷泉区的西北方向.";
					if (.@compass_check)
						viewpoint 1,134,221,8,0xFF00FF;
					break;
				case 6:
					mes "[普隆德拉导游]";
					mes "在首都中一共有两个旅馆,";
					mes "坐落于中央喷泉区的东侧和西侧.";
					if (.@compass_check) {
						viewpoint 1,204,191,9,0xFF00FF;
						viewpoint 1,107,192,10,0xFF00FF;
					}
					break;
				case 7:
					mes "[普隆德拉导游]";
					mes "贸易站位于中央喷泉区的东南方向.";
					if (.@compass_check)
						viewpoint 1,179,184,11,0x00FF00;
					break;
				case 8:
					mes "[普隆德拉导游]";
					mes "酒馆位于中央喷泉区的东南方向, 在贸易站的东南边.";
					if (.@compass_check)
						viewpoint 1,208,154,12,0x00FF00;
					break;
				case 9:
					mes "[普隆德拉导游]";
					mes "您若站在中央喷泉区往北看,";
					mes "会发现左右两边有两块空白的区域.";
					mes "站在那里, 把头朝向东边或西边,";
					mes "你就可以看到图书馆啦.";
					if (.@compass_check) {
						viewpoint 1,120,267,13,0x00FF00;
						viewpoint 1,192,267,14,0x00FF00;
					}
					break;
				case 10:
					mes "[普隆德拉导游]";
					mes "职业介绍所位于中央喷泉区的西南方向.";
					if (.@compass_check)
						viewpoint 1,133,183,15,0x00FF00;
					break;
				case 11:
					mes "[普隆德拉导游]";
					mes "城堡位于普隆德拉的正北方向.";
					mes "从城堡的后门可以普隆德拉北部区域.";
					if (.@compass_check)
						viewpoint 1,156,360,16,0x00FF00;
					break;
				case 12:
					mes "[普隆德拉导游]";
					mes "城市会所位于普隆德拉西南方向的角落.";
					mes "那里有染发师, 能改变你头发颜色.";
					if (.@compass_check)
						viewpoint 1,75,91,17,0x01FF01;
					break;
				case 13:
					mes "[普隆德拉导游]";
					mes "如果您不需要在小地图上显示标记的话,";
					mes "请让我帮您'删除小地图的标记'.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,4,0x00FF00;
			viewpoint 2,237,41,5,0x0000FF;
			viewpoint 2,46,345,6,0x00FF00;
			viewpoint 2,175,220,7,0xFF0000;
			viewpoint 2,134,221,8,0xFF0000;
			viewpoint 2,204,191,9,0xFF0000;
			viewpoint 2,107,192,10,0xFF0000;
			viewpoint 2,179,184,11,0x00FF00;
			viewpoint 2,208,154,12,0x00FF00;
			viewpoint 2,120,267,13,0x00FF00;
			viewpoint 2,192,267,14,0x00FF00;
			viewpoint 2,133,183,15,0x00FF00;
			viewpoint 2,156,360,16,0x00FF00;
			viewpoint 2,75,91,17,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[普隆德拉导游]";
			mes "高新技术改进了我们的信息系统,";
			mes "我们现在可以在您的小地图中打上标记,";
			mes "让您更容易找到目的地.";
			next;
			mes "[普隆德拉导游]";
			mes "小地图位于您的屏幕右上方.";
			mes "如果您看不到它的话, 请按 Ctrl+Tab";
			mes "或在基础信息面板中点击'地图'按钮.";
			next;
			mes "[普隆德拉导游]";
			mes "在您的小地图上, 可以通过点击上面的";
			mes "'+' 和 '-' 符号来缩放小地图.";
			mes "希望您能享受在这里度过的美好时光.";
			break;
		case 4:
			mes "[普隆德拉导游]";
			mes "好的, 冒险家...";
			mes "祝您在卢恩米德加兹王国的旅行";
			mes "过得有趣且平安.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

prontera,282,208,2	duplicate(PrtGuide)	东门 - 导游	105

prontera,29,200,6	duplicate(PrtGuide)	西门 - 导游	105

prontera,160,29,0	duplicate(PrtGuide)	南门 - 导游	105

prontera,151,330,4	duplicate(PrtGuide)	北门 - 导游	105
